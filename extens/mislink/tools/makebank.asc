10 key off : hide : curs off : click off : mode 0 : update off 
15 MXF=199 : rem> Max No of files <
20 reserve as work 9,1280 : wash start(9),0,0,320,8
40 dim LON(24),NM$(MXF),SZ(MXF)
42 MSK$="*.*" : NOHD=true : DELF=false
45 reserve as work 8,MXF*8+8+6+8 : fill start(8) to start(8)+length(8),0 : doke start(8)+4,$ABCD : T$="FBNK" : loke start(8),leek(varptr(T$))
46 F=free-8000
47 reserve as work 10,F
48 fill start(10) to start(10)+MXF*8+14,0
49 MXSZ=length(10)-(MXF*8+14)-1024
50 for T=0 to 24 : LON(T)=false : next T
60 palette $0,$667,$567,$654,$774,$710,$300 : wait vbl 
70 for T=0 to 24 : copy logic+32000,logic+32032 to start(9)+T*32 : next T
80 hide on : wipe back : wipe physic : clw : curs off : ink 2 : draw 0,9 to 319,9
85 on error goto 10000
90 restore 340 : Y=0 : AD=false
110 read NL : repeat 
120 for T=0 to NL-1 : OY=Y : read F$,X,Y,CL
130 if X=-1 then X=(40-len(F$))/2
140 if Y=-1 then Y=OY+1
150 LON(Y)=true
160 F$=F$+chr$(0)
170 for Q=0 to 3 : if btst(Q,CL) then text logic+Q*2,start(5),varptr(F$),X,Y
180 next Q
190 next T
200 read NL : until NL=-1
201 T1$=MSK$+chr$(0)
202 if NOHD then T2$="Yes"+chr$(0) else T2$="No "+chr$(0)
203 if DELF then T3$="Yes"+chr$(0) else T3$="No "+chr$(0)
204 text logic+4,start(5),varptr(T1$),23,12
205 text logic+4,start(5),varptr(T2$),30,13
206 text logic+4,start(5),varptr(T3$),28,14
207 T$="Max files:"+chr$(0) : text logic+2,start(5),varptr(T$),0,22
208 T$="Max bank size:    Kb"+chr$(0) : text logic+2,start(5),varptr(T$),18,22
209 TP=string(MXF+1) : text logic,start(5),TP,10,22
210 TP=string(MXSZ/1024) : text logic,start(5),TP,32,22
212 D=(deek(start(8)+4)=$ABCD) : LON(5)=not(D)
213 T$="Total files:"+chr$(0) : text logic+2,start(5),varptr(T$),0,23
214 T$="Total size:    Kb"+chr$(0) : text logic+2,start(5),varptr(T$),18,23
215 if D then TSZ=0 : TF=0 : goto 217
216 TF=deek(start(8)+4) : TSZ=leek(start(8)+TF*8+6)+leek(start(8)+TF*8+4+6)+TF*8+8+6 : inc TF
217 TP=string(TF) : text logic,start(5),TP,12,23
218 TP=string(TSZ/1024) : text logic,start(5),TP,29,23
219 LON(10)=false
220 Y=y mouse : Y2=y mouse/8 : OY=Y2 : OOY=OY
225 palsplit 1,start(9),1,8,24
230 repeat 
240 doke start(9)+32*OY,$0
250 OY=Y2 : Y2=y mouse/8
260 if Y2<2 then Y2=2
265 if Y2>23 then Y2=23
270 if LON(Y2) then CL=$710 : else CL=$300
275 doke start(9)+32*Y2,CL
290 wait vbl 
300 until mouse key=1 and LON(Y2)
302 palsplit 0,0,0,0,0 : doke start(9)+32*Y2,0 : wait vbl 
305 if Y2=4 then goto 1000 : rem> Load FBNK <
306 if Y2=5 then goto 2000 : rem> Save FBNK <
307 if Y2=7 then goto 3000 : rem> Add whole dir <
308 if Y2=8 then goto 4000 : rem> Add single <
309 if Y2=10 then goto 5000 : rem> Remove <
310 if Y2=12 then goto 6000 : rem> New mask <
311 if Y2=13 then NOHD=not(NOHD) : for T=1 to 5 : wait vbl : next T : goto 201
312 if Y2=14 then DELF=not(DELF) : for T=1 to 5 : wait vbl : next T : goto 201
315 default : end 
320 rem
330 rem
340 data 10
350 data "Make File-bank",-1,0,1
360 data "....Load an FBNK....",-1,4,2
370 data "....Save an FBNK....",-1,-1,2
380 data "...Add directory....",-1,7,3
390 data ".....Add a file.....",-1,-1,3
400 data "....Remove files....",-1,10,2
410 data "Search mask :",9,12,3
420 data "Strip .MBK headers :",9,-1,3
430 data "Delete old files :",9,-1,3
435 data "........Quit........",-1,16,2
440 data-1
997 rem
998 rem> Load FBNK <
999 rem
1000 show on : F$=file select$("*.BNK","Load a file-bank",4)
1010 if F$="" then goto 80
1020 G$=dir first$(F$,-1) : if G$="" then boom : goto 1000
1030 hide on 
1032 locate 0,2 : print "Loading..."
1040 T$=F$+chr$(0) : Q$=space$(10)
1050 R=dload(varptr(T$),varptr(Q$),0,6)
1060 if R<>6 then boom : print "Disk error!" : wait key : goto 80
1070 if left$(Q$,4)<>"FBNK" then boom : print "Not a file-bank!" : wait key : goto 80
1080 CF=deek(varptr(Q$)+4) : if CF>=MXF then boom : print "Too many files!" : wait key : goto 80
1085 LHD=CF*8+8+6
1090 R=dload(varptr(T$),start(8),0,LHD)
1100 if R<>LHD then boom : print "Disk error!" : wait key : goto 80
1105 S8=start(8) : H=6 : for T=0 to CF : loke S8+H,leek(S8+H)-LHD : H=H+8 : next T
1110 BSZ=leek(start(8)+CF*8+6)+leek(start(8)+CF*8+4+6)
1120 R=dload(varptr(T$),start(10),LHD,BSZ)
1140 goto 80
1997 rem
1998 rem> Save FBANK <
1999 rem
2000 if deek(start(8)+4)=$ABCD then boom : goto 80
2005 show on : F$=file select$("*.BNK","Save the file-bank",8)
2010 if F$="" then goto 80
2015 L=0 : G$=dir first$(F$,-1) : if G$="" then goto 2020
2016 open in #1,F$ : L=lof(#1) : close #1
2020 locate 0,1 : print "Saving..." : D=dfree
2025 CF=deek(start(8)+4)
2026 LHD=CF*8+8+6
2030 BSZ=leek(start(8)+CF*8+6)+leek(start(8)+CF*8+4+6)+8
2040 if BSZ+LHD>D-L then boom : print "Not enough disk space!" : wait key : goto 80
2045 if not(AD) then S8=start(8) : H=6 : for T=0 to CF : loke S8+H,leek(S8+H)+LHD : H=H+8 : next T : AD=true
2050 on error goto 2070
2060 bsave F$,start(8) to start(8)+LHD : goto 2080
2070 boom : print "Disk error!" : wait key : on error goto 10000 : goto 2115
2080 on error goto 10000
2090 F$=F$+chr$(0)
2100 R=dsave(varptr(F$),start(10),LHD,BSZ)
2115 S8=start(8) : H=6 : for T=0 to CF : loke S8+H,leek(S8+H)-LHD : H=H+8 : next T : AD=false
2120 goto 80
2997 rem
2998 rem> Add whole dir <
2999 rem
3000 show on : F$=file select$(MSK$,"        Select directory.         Enter any filename to abort.",4)
3010 if F$<>"" then goto 80
3020 T=0 : TSZ=0
3030 F$=dir first$(MSK$,-1)
3040 while F$<>""
3045 TP=val(mid$(F$,42,3)) : if TP=16 then goto 3075
3050 NM$(T)=left$(F$,12)-" "
3060 SZ(T)=val(mid$(F$,13,8)) : if SZ(T) mod 2=1 then inc SZ(T)
3062 if right$(NM$(T),4)=".MBK" and NOHD then SZ(T)=SZ(T)-18
3065 TSZ=TSZ+SZ(T)
3070 inc T
3075 F$=dir next$
3080 wend 
3090 NF=T-1
3100 CF=deek(start(8)+4) : if CF=$ABCD then CF=0 : FT=true : bell : else FT=false
3110 OFS=6+CF*8
3120 H=leek(start(8)+OFS)+leek(start(8)+OFS+4)
3125 if H+TSZ>=MXSZ then boom : print "Not enough memory!" : wait key : goto 80
3130 if not(FT) then OFS=OFS+8 : inc CF
3135 CF=CF+NF : if CF>=MXF then boom : print "Too many files!" : wait key : goto 80
3136 doke start(8)+4,CF
3137 hide on : home : pen 3 : print "Found";NF+1;" files." : print "Total files in bank will be";CF+1 : print "Total size of bank will be";H+TSZ
3138 X=xcurs : Y=ycurs : T$=string$(".",NF+1) : pen 5 : print T$ : pen 4
3140 for T=0 to NF
3150 bload NM$(T),start(10)+H : if right$(NM$(T),4)=".MBK" and NOHD then doke $FF8240,$2 : copy start(10)+H+18,start(10)+H+18+SZ(T) to start(10)+H : doke $FF8240,0
3160 loke start(8)+OFS,H : loke start(8)+OFS+4,SZ(T)
3170 H=H+SZ(T) : OFS=OFS+8
3175 locate X,Y : print "." : inc X : if X>39 then X=0 : inc Y
3176 if DELF then kill NM$(T)
3180 next T
3190 goto 80
3997 rem
3998 rem> Add single <
3999 rem
4000 show on : F$=file select$(MSK$,"Load a file",4)
4010 if F$="" then goto 80
4020 G$=dir first$(F$,-1) : if G$="" then boom : goto 4000
4030 CF=deek(start(8)+4) : if CF=$ABCD then FT=true : CF=0 : else FT=false
4040 H=leek(start(8)+6+CF*8)+leek(start(8)+6+CF*8+4)
4050 OFS=CF*8+6 : if not(FT) then OFS=OFS+8
4060 open in #1,F$ : L=lof(#1) : close #1
4062 if right$(F$,4)=".MBK" and NOHD then L=L-18
4065 if L mod 2=1 then inc L
4070 if H+L>=MXSZ then boom : print "Not enough memory!" : wait key : goto 80
4080 bload F$,start(10)+H
4085 if NOHD and right$(F$,4)=".MBK" then copy start(10)+H+18,start(10)+H+L to start(10)+H
4090 loke start(8)+OFS,H : loke start(8)+OFS+4,L
4100 if not(FT) then inc CF
4105 doke start(8)+4,CF
4106 if DELF then kill F$
4110 goto 80
5997 rem
5998 rem> Enter new mask <
5999 rem
6000 locate 23,12 : print space$(17)
6010 locate 22,12 : input "";T$
6015 locate 23,12 : print space$(17)
6020 if len(T$)>12 or len(T$)=0 then goto 201
6030 MSK$=lower$(T$) : goto 201
10000 clw : curs off 
10010 print "There has been an error!" : print "Number";errn;" in line";errl
10020 print "Emergency save (Y/N)?"
10030 repeat : G$=upper$(inkey$) : until G$="Y" or G$="N"
10040 if G$="Y" then goto 2000
10050 default : end 

Reserved memory banks:
 5:  chr set 0x000900
