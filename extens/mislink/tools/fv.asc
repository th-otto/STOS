10 key off : hide : flash off : click off : mode 1 : curs off : auto back off : update off 
12 palette $0,$770,$544,$777
15 TXT=true
20 F=free : if F>500000 then F=500000
30 reserve as work 10,F
40 clw : curs off : home : centre "File reader v1.0"
50 print : centre "Wheee the fibble"
60 show on : F$=file select$("*.*","Load a file to examine",4)
70 if F$="" then default : end 
80 open in #1,F$ : L=lof(#1) : close #1
90 if L>F then print "File is too large!" : wait key : goto 40
100 bload F$,10
110 H=0
120 clw : curs off 
130 home : centre "Use curs up/down to move, space to exit and ESC to toggle ascii/hex"
140 for T=0 to 19 : locate T*3+4,2 : print T; : next T
150 for T=0 to 19 : locate 0,T+4 : print T; : next T
160 gosub 1000
170 repeat : G$=inkey$ : SC=scancode : until SC=1 or SC=72 or SC=80 or SC=57
180 if SC=57 then goto 40
185 if SC=1 then TXT=not(TXT)
190 if SC=72 then H=H-400 : if H<0 then H=0
200 if SC=80 then H=H+400 : if H>L-400 then H=L-400
210 goto 160
997 rem
998 rem> Display 400 bytes <
999 rem
1000 T=0
1010 for Y=0 to 19 : for X=0 to 19
1020 P=peek(start(10)+H+T)
1030 T$=hex$(P)-"$" : pen 2
1040 if TXT and P>31 then T$=chr$(P) : pen 3
1050 locate X*3+4,Y+4 : print T$;" "
1060 inc T
1070 next X : next Y
1080 pen 1 : return 
