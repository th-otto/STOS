10 rem** this program grabs sprites from a picture in blocks of 32
15 reserve as screen 5 : cls 5
30 click off : curs off : key off : mode 0
40 XOFF=0 : YOFF=0 : XIN=(peek(start(1)+26)*16) : YIN=peek(start(1)+27) : AM=deek(start(1)+16) : HIG=YIN : WID=XIN : AMO=AM : DR=1
50 goto 360
60 rem **
70 rem ** grab sprites
80 rem **
90 hide : get palette (5)
100 screen copy 5 to logic : screen copy 5 to back
110 AM=deek(start(1)+16)
120 X=XOFF : Y=YOFF : SCO=1 : XLI=(320-(320 mod XIN)) : YLI=(200-(200 mod YIN))
130 get sprite X,Y,SCO
140 if DR=1 then X=X+XIN : if X>=XLI then X=XOFF : Y=Y+YIN
150 if DR=2 then Y=Y+YIN : if Y>=YLI then Y=YOFF : X=X+XIN
160 inc SCO : if SCO<AM+1 then goto 130
170 C=hunt(start(1) to start(1)+length(1),"PALT")
180 if C<>0 then C=C+4 : copy start(5)+32000,start(5)+32032 to C
190 boom : wait key : show : return 
200 rem **
210 rem ** show sprites
220 rem **
230 cls : show : X=4
240 windopen 1,0,0,20,10,0,3 : curs off : locate 0,2 : centre "Use mouse keys"
250 locate 0,4 : centre "To veiw sprites"
260 C=hunt(start(1) to start(1)+length(1),"PALT")+4 : copy C,C+32 to logic+32000 : get palette (logic)
270 change mouse X
280 if mouse key=2 and X<AM+4 then inc X
290 if mouse key=1 and X>4 then dec X : 
300 Z$=inkey$ : if Z$=" " then return 
310 home print X-3," "
320 wait 5 : goto 270
330 rem **
340 rem ** main menu
350 rem **
360 cls : windopen 1,0,0,20,12,0,3 : curs off 
370 palette 0,$210,$320,$430,$540,$650,$770,$650,$540,$430,$320,$210,$100
380 colour 15,1911 : C=1 : restore 410 : for Y=0 to 10
390 read A$ : locate 0,Y : pen C : print A$ : inc C
400 next Y : pen 15
410 data "Load picture PI1"
420 data "Load picture NEO"
430 data "Grab sprites"
440 data "Alter 'Y' offset"
450 data "Alter 'X' offset"
460 data "Alter 'Y' increment"
470 data "Alter 'X' increment"
480 data "Save sprites"
490 data "View sprites"
500 data "Build sprite bank"
510 data "Grab direction"
520 data "End"
530 if mouse key=1 then ICN=(y mouse/16)+1 windel 1 : goto 550
535 Z$=inkey$ : if Z$=" " then fade 3 : wait 25 : default : end 
540 wait vbl : goto 530
550 on ICN gosub 570,600,630,640,680,720,760,800,850,860,865
560 goto 360
570 F$=file select$("*.pi1","LOAD picture.PI1")
580 if F$="" then return 
590 load F$,5 : return 
600 F$=file select$("*.neo","LOAD picture.NEO")
610 if F$="" then return 
620 load F$,5 : return 
630 gosub 90 : return 
640 cls : pen 7 : print "Y offset" : print "Use mousekeys"
650 repeat : wait vbl : locate 0,4 : pen 4 : print YOFF : if mouse key=2 and YOFF<320 then inc YOFF
660 if mouse key=1 and YOFF>0 then dec YOFF
670 Z$=inkey$ : until Z$=" " : return 
680 cls : pen 7 : print "x offset" : print "Use mousekeys"
690 repeat : wait vbl : locate 0,4 : pen 4 : print XOFF : if mouse key=2 and XOFF<320 then inc XOFF
700 if mouse key=1 and XOFF>0 then dec XOFF
710 Z$=inkey$ : until Z$=" " : return 
720 cls : pen 7 : print "Y increment" : print "Use mousekeys"
730 repeat : wait vbl : locate 0,4 : pen 4 : print YIN : if mouse key=2 and YIN<320 then inc YIN
740 if mouse key=1 and YIN>0 then dec YIN
750 Z$=inkey$ : until Z$=" " : return 
760 cls : pen 7 : print "X increment" : print "Use mousekeys"
770 repeat : wait vbl : locate 0,4 : pen 4 : print XIN : if mouse key=2 and XIN<320 then inc XIN
780 if mouse key=1 and XIN>0 then dec XIN
790 Z$=inkey$ : until Z$=" " : return 
800 F$=file select$("*.mbk","SAVE sprites")
810 if F$="" then return 
820 DT=0 : for N=1 to len(F$) : if mid$(F$,N,1)="." then DT=N
830 next N : if DT=0 then I$=F$+".mbk" else I$=mid$(F$,1,N)+"mbk"
840 save I$,1 : return 
850 gosub 230 : return 
860 gosub 910 : return 
865 cls : pen 7 : print "Choose grab direction" : print "press...." : print "left mousekey for horizontal" : print "or" : print "right mousekey for vertical"
866 if mouse key=1 then DR=1 : return 
867 if mouse key=2 then DR=2 : return 
868 goto 866
880 rem **
890 rem ** make sprite bank menu
900 rem **
910 windopen 1,0,0,20,12,0,3 : curs off 
920 Y=0 : C=1 : restore 930 : repeat : read A$ : locate 0,Y : pen C : print A$ : inc Y : inc C : until A$="Return to Menu"
930 data "WIDTH..."
940 data "HEIGHT.."
950 data "AMOUNT.."
960 data "MAKE SPRITES"
970 data "Return to Menu"
980 locate 9,0 : print WID;" "
990 locate 9,1 : print HIG;" "
1000 locate 9,2 : print AMO;" "
1010 if mouse key=1 then ICN=(y mouse/16)+1 : goto 1040
1020 if mouse key=2 then ICN=(y mouse/16)+1 : goto 1140
1030 wait vbl : goto 980
1040 on ICN goto 1050,1070,1090,1110,1130
1050 if WID>16 then WID=WID-16
1060 goto 920
1070 if HIG>1 then dec HIG
1080 goto 920
1090 if AMO>1 then dec AMO
1100 goto 920
1110 gosub 1250
1120 goto 920
1130 windel 1 : return 
1140 on ICN goto 1150,1170,1190,1210,1230
1150 if WID<64 then WID=WID+16
1160 goto 920
1170 if HIG<64 then inc HIG
1180 goto 920
1190 if AMO<100 then inc AMO
1200 goto 920
1210 gosub 1250
1220 goto 920
1230 windel 1 : return 
1240 remWID=peek(start(1)+26)*16 : AMO=deek(start(1)+16) : HIG=peek(start(1)+27)
1250 BLK=(((WID/16)*HIG)*10)
1260 MISC=(AMO*8)
1270 LEG=(BLK*AMO)+MISC+56
1280 if LEG>free-5000 then boom : locate 0,10 : print "FAR to big!" : return 
1290 erase 1 : reserve as data 1,LEG : SBK=start(1) : loke SBK,$19861987 : loke SBK+4,18
1300 doke SBK+16,AMO
1310 restore 1330 : I=SBK+MISC+22 : for N=0 to 4 : read A : poke I+N,A : next N
1320 I=I+4 : copy start(5)+32000,start(5)+32032 to I
1330 data 80,65,76,84,84
1340 X=22 : OFSET=MISC+36 : for N=0 to AMO-1 : loke SBK+X,OFSET
1350 poke SBK+X+4,(WID/16)
1360 poke SBK+X+5,HIG
1370 poke SBK+X+6,0
1380 poke SBK+X+7,0
1390 X=X+8 : OFSET=OFSET+BLK : inc CO1 : next N
1400 XOFF=0 : YOFF=0 : XIN=(peek(start(1)+26)*16) : YIN=peek(start(1)+27) : AM=deek(start(1)+16) : HIG=YIN : WID=XIN : AMO=AM
1410 boom : return 
1420 rem LEG=((((WID/16)*HIG)*10)*AMO)+(AMO*8)+52

Reserved memory banks:
 1:  sprites 0x000100
