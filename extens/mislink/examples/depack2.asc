10 key off : hide : curs off : click off : mode 0 : update off 
15 MXF=112 : dim FL$(MXF)
20 reserve as work 10,free-32000
30 palette 0,$567,$765,$654,$543
40 windopen 1,0,0,40,12,0,4
42 restore : clw : curs off 
45 for T=1 to 4 : read T$,X,Y
50 pen T : locate X,Y : print T$
60 next T
70 pen 1 : show on 
80 repeat : V=val(inkey$) : Y=(y mouse/16)-1
100 until mouse key=1 or V>0
110 if mouse key=1 then V=Y
120 on V goto 200,400,140
130 goto 80
140 default : end 
197 rem
198 rem> Single <
199 rem
200 clw : curs off : F$=file select$("*.*","Select a file to depack",4)
210 if F$="" then goto 42
220 G$=F$+chr$(0)
230 R=real length(varptr(G$))
240 if R=0 then boom : print "Not packed with a recognised packer!" : wait key : goto 42
250 bload F$,10
260 L=depack(start(10))
270 print "Depacked size=";L
280 G$=file select$("*.*","Save file to...",8)
290 if G$="" then print "Cancel save?"; : repeat : R$=upper$(inkey$) : until R$="Y" or R$="N" : print R$ : if R$="Y" then goto 42 : else G$=F$
300 bsave G$,start(10) to start(10)+L
310 goto 42
397 rem
398 rem> Batch <
399 rem
400 clw : curs off : F$=file select$("*.*","Select source directory and extension (. for *.*)",8)
402 SD$=dir$ : SD=drive : if SD$="" then SD$="\"
405 Q$=file select$("*.*","Select destination directory",8)
406 DD$=dir$ : DD=drive : if DD$="" then DD$="\"
407 drive=SD : dir$=SD$
410 if F$="" then goto 42
420 if left$(F$,1)<>"." then goto 400
430 if F$="." then F$="*.*" : goto 450
440 F$=("*"+F$)-" "
450 home : print "Reading directory..."
460 T=0
470 G$=dir first$(F$,-1)
480 if G$="" then boom : print "No matches!" : wait key : goto 42
490 repeat 
500 FL$(T)=left$(G$,12)-" "
510 TF$=FL$(T)+chr$(0)
520 SZ=real length(varptr(TF$))
530 if SZ=0 then print "File : ";FL$(T);" skipped" : goto 550
540 inc T
550 G$=dir next$
560 until G$=""
570 NF=T-1 : if NF=-1 then boom : print "No files to depack!" : wait key : goto 42
575 clw : curs off 
590 for T=0 to NF
592 home : pen 1 : print T+1;" of";NF+1
595 pen 2 : locate 0,1 : print "Loading                 "
600 bload FL$(T),10
605 pen 3 : locate 0,1 : print "Depacking"
610 L=depack(start(10))
620 drive=DD : dir$=DD$
625 pen 4 : locate 0,1 : print "Saving (";L/1024;"k )"
630 bsave FL$(T),start(10) to start(10)+L
635 drive=SD : dir$=SD$
640 next T
650 goto 42
997 rem
998 rem> Menu text <
999 rem
1000 data "The Missing Link Depacker. v1.0",5,0
1010 data "1..............Depack single file",4,2
1020 data "2....................Depack batch",4,3
1030 data "3............................Exit",4,4

Reserved memory banks:
 5:  chr set 0x001000
