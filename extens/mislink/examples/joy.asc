10 rem **
20 rem ** Example of all joystick commands
30 rem **
40 on error goto 410
50 curs off : click off : key off : hide : mode 0
60 if length(5)=0 then load "bob.mbk",5
65 if length(6)=0 then load "digi.mbk",6
70 p on 
80 bob 0,0,320,200,0,1
90 dim X(1),Y(1),I(1),OX(1,1),OY(1,1)
100 A=palt(start(5))
110 logic=back
120 X(0)=64 : Y(0)=25 : I(0)=1 : X(1)=240 : Y(1)=159 : I(1)=2 : NC=0 : HIT=0
130 SC=0 : for N=0 to 1 : for F=0 to 1 : OX(N,F)=0 : OY(N,F)=0 : next F : next N
140 rem **
150 rem ** Main loop
160 rem **
170 TX=OX(1,SC) : TY=OY(1,SC) : wash logic,TX,TY,TX+32,TY+16
180 TX=OX(0,SC) : TY=OY(0,SC) : wash logic,TX,TY,TX+32,TY+16
190 OX(1,SC)=boundary(X(1)) : OY(1,SC)=Y(1)
200 OX(0,SC)=boundary(X(0)) : OY(0,SC)=Y(0)
210 P=p joy(1) : TP=string(P) : text logic,0,TP,0,0
220 P=p joy(0) : TP=string(P) : text logic,0,TP,10,0
230 bob logic,start(5),I(1),X(1),Y(1),0
240 bob logic,start(5),I(0),X(0),Y(0),0
250 if p left(1) and X(1)>0 then dec X(1)
260 if p left(0) and X(0)>0 then dec X(0)
270 if p right(1) and X(1)<304 then inc X(1)
280 if p right(0) and X(0)<304 then inc X(0)
290 if p up(1) and Y(1)>0 then dec Y(1)
300 if p up(0) and Y(0)>0 then dec Y(0)
310 if p down(1) and Y(1)<183 then inc Y(1)
320 if p down(0) and Y(0)<183 then inc Y(0)
330 if p fire(1) then NC=$707
340 if p fire(0) then NC=$77
350 if overlap(X(0),Y(0),X(1),Y(1),16,16,16,16) then NC=$700 : digi play 1,start(6),1,5,0
360 Z$=inkey$ : C=scancode : if C=57 then p stop : digi play 0,0,0,0,0 : default : end 
370 SC=SC xor 1
380 screen swap : wait vbl 
390 doke $FFFF8240,0 : doke $FFFF8240,NC : NC=0
400 goto 170
410 p stop 
