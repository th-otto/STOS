10 rem **
20 rem ** Example of world command
30 rem **
40 rem **
50 if length(5)=0 then load "world.mbk",5
60 if length(6)=0 then load "worldmap.mbk",6
70 if length(7)=0 then load "bob.mbk",7
80 A=palt(start(5))
90 SP=4
100 world 0,16,320,176,0,1
110 bob 0,16,320,176,0,1
120 X=0 : Y=16 : BX=144 : BY=100
130 LW=x limit(start(6),0,288)
140 LH=y limit(start(6),16,176)
150 S5=start(5) : S6=start(6) : S7=start(7)
160 logic=back
170 rem **
180 rem ** Main loop
190 rem **
200 Z$=inkey$ : if Z$=" " then default : stop 
210 TX=X+BX : TY=Y+BY : B=which block(S6,TX+16,TY+16) : if B=15 then set block S6,TX+16,TY+16,0
220 if jright then A=which block(S6,X+BX+32,Y+BY) : if A=0 or A=15 then X=X+SP : if X>LW then X=LW
230 if jleft then A=which block(S6,X+BX,Y+BY) : if A=0 or A=15 then X=X-SP : if X<0 then X=0
240 if jup then A=which block(S6,X+BX+8,Y+BY-8) : if A=0 or A=15 then Y=Y-SP : if Y<=0 then Y=0 : BY=BY-4 : if BY<16 then BY=16
250 if jdown then A=which block(S6,X+BX+8,Y+BY+8) : if A=0 or A=15 then Y=Y+SP : if Y>LH then Y=LH : BY=BY+4 : if BY>160 then BY=160
260 world logic,S5,S6,X,Y,0
270 bob logic,S7,0,BX,BY,99
280 screen swap : wait vbl : goto 200
