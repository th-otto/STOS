10 rem **
20 rem ** Example of overlap command
30 rem **
40 curs off : click off : key off : hide : mode 0
50 if length(5)=0 then load "bob.mbk",5
55 if length(6)=0 then load "digi.mbk",6
60 dim S(360)
70 for A=0 to 360 : S(A)=sin(rad(A))*cos(rad(A)*3)*144+144 : next A
80 XI=0 : YI=90 : BIM=0
90 BW=b width(start(5),BIM)
100 BH=b height(start(5),BIM)
110 bob 0,0,320,200,0,1
120 A=palt(start(5))
130 logic=back
140 rem **
150 rem ** Main loop
160 rem **
170 X=x mouse : Y=y mouse : wipe logic
180 bob logic,start(5),1,X,Y,0
190 TX=S(XI) : TY=S(YI)
200 bob logic,start(5),BIM,TX,TY,0
210 inc XI : if XI>359 then XI=0
220 inc YI : if YI>359 then YI=0
230 HIT=overlap(X,Y,TX,TY,16,16,BW,BH)
240 Z$=inkey$ : C=scancode : if C=57 then default : end 
250 screen swap : wait vbl 
260 doke $FFFF8240,0 : if HIT=true then doke $FFFF8240,$700 : digi play 1,start(6),1,5,0
270 goto 170
