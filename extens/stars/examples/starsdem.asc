10 rem  **********************************************************************
20 rem  *                                                                    *
30 rem  * The ST(e)/TT/Falcon 030 Stars Extension Demo in ST Low Resolution. *
40 rem  *                                                                    *
50 rem  *             Original       :   12/11/1997                          *
60 rem  *             Last edited    :   02/04/1998                          *
70 rem  *                                                                    *
80 rem  *           ½ 1997, 1998 Anthony Hoskin.                             *
90 rem  *                        45 Wythburn Rd,                             *
100 rem *                        Newbold,                                    *
110 rem *                        Chesterfield,                               *
120 rem *                        Derbyshire,                                 *
130 rem *                        S41 8DP  (U.K.)                             *
140 rem *                                                                    *
150 rem *   This demo will only work when the following extension(s) are     *
160 rem *   installed :-                                                     *
170 rem *                                                                    *
180 rem *       ST(e)/TT/Falcon 030 STARS => v0.8 by Anthony Hoskin.         *
190 rem *                                                                    *
200 rem **********************************************************************
210  : 
220 rem ~~~~~~~~~~~~~~~~~ Initialise Screen mode ~~~~~~~~~~~~~~~~~~~~~~~~~
230  : 
240 on error goto 1150
250 key off : rem Turn off the STOS editor window.
260 hide on : rem HIDE the STOS mouse.
270 curs off : rem Turn of the flashing cursor.
280 if mode<2 then mode 0 else goto 640
290  : 
300 rem ~~~~~~~~~~~~~~~~ Initialise palette ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
310  : 
320 palette $0,$255,$277,$233,$131,$420,$430,$450,$750,$773,$70,$373,$277,$337,$737,$377
330  : 
340 rem ~~~~~~~~~~~~~~~~ Initialise Variables ~~~~~~~~~~~~~~~~~~~~~~~~~~~
350  : 
360 SCR_WIDTH=320 : SCR_HEIGHT=200
370 COLR1=1 : rem 3D effect background fill colour
380 COLR2=2 : rem 3D effect highlight colour
390 COLR3=3 : rem 3D effect shadow colour
400 COLR4=4 : rem Text colour
410  : 
420 rem ~~~~~~~~~~~~~~~~ Initialise screen ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
430  : 
440 gosub 700 : rem Draw main screen display
450 screen copy physic to back
460  : 
470 rem ~~~~~~~~~~~~~~~ Main activity loop ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
480  : 
490 set stars 200,2,W_X,W_Y,W_X+W_WIDTH,W_Y+W_HGT,1,15
500 wipe stars on 
510  : 
520 X=0 : Y=0
530 repeat 
540  : go stars X,Y,logic
550  : wait vbl 
560  : if jup then dec Y
570  : if jdown then inc Y
580  : if jleft then dec X
590  : if jright then inc X
600 until (mouse key>0) or (inkey$<>"")
610  : 
620 rem ~~~~~~~~~~~~~~~ Terminate program tidily ~~~~~~~~~~~~~~~~~~~~~~~~~
630  : 
640 default : clear key : end 
650  : 
660 rem ===================================================================
670 rem Draw main screen display.
680 rem ===================================================================
690  : 
700 X1=0 : Y1=0 : X2=SCR_WIDTH-1 : Y2=SCR_HEIGHT-1
710 ink COLR1 : bar X1,Y1 to X2,Y2
720 ink COLR2 : draw X1,Y2 to X1,Y1 : draw X1,Y1 to X2,Y1
730 ink COLR3 : draw X1,Y2 to X2,Y2 : draw X2,Y2 to X2,Y1
740 W_X=(X2/2)+8 : W_WIDTH=SCR_WIDTH-(W_X+16)
750 W_Y=Y1+24 : W_HGT=SCR_HEIGHT-(W_Y*2)
760 ink COLR3 : draw W_X-1,W_Y+W_HGT+1 to W_X-1,W_Y-1
770 draw W_X-1,W_Y-1 to W_X+W_WIDTH+1,W_Y-1
780 ink COLR2 : draw W_X-1,W_Y+W_HGT+1 to W_X+W_WIDTH+1,W_Y+W_HGT+1
790 draw W_X+W_WIDTH+1,W_Y+W_HGT+1 to W_X+W_WIDTH+1,W_Y-1
800 pen COLR4 : paper COLR1
810  : 
820 rem ===================================================================
830 rem Clear main window display.
840 rem ===================================================================
850  : 
860 ink COLR0 : bar W_X,W_Y to W_X+W_WIDTH,W_Y+W_HGT
870  : 
880 locate 1,1 : centre "The new STARS Extension Demo"
890 locate 1,3 : print "Here you see the"
900 locate 1,4 : print "stars being drawn"
910 locate 1,5 : print "inside a window"
920 locate 1,6 : print "and the WIPESTARS"
930 locate 1,7 : print "command only acts"
940 locate 1,8 : print "on the stars in"
950 locate 1,9 : print "the area bounded"
960 locate 1,10 : print "by the coords as"
970 locate 1,11 : print "specified in the"
980 locate 1,12 : print "SET STARS command."
990  : 
1000 locate 1,14 : print "This means that it"
1010 locate 1,15 : print "is now unnecessary"
1020 locate 1,16 : print "to redraw the whole"
1030 locate 1,17 : print "screen after each"
1040 locate 1,18 : print "starfield draw."
1050 locate 1,20 : print "Use the joystick"
1060 locate 1,21 : print "to move around the"
1070 locate 1,22 : print "starfield. Press"
1080 locate 1,23 : print "any key to quit."
1090 return 
1100  : 
1110 rem ==================================================================
1120 rem General purpose error handler.
1130 rem ==================================================================
1140  : 
1150 on error goto 0 : wait vbl 
1160 physic=default physic : logic=default logic : back=default back
1170 break on : if mode<2 then mode 1
1180 print "Error #"+str$(errn)-" "; : print " at line #"+str$(errl)-" "
1190 print : print "Press any key to Quit."
1200 wait key : resume 640
1210 end 
