10 rem  **********************************************************************
20 rem  *                                                                    *
30 rem  * A demo program showing the use of GEM/GDOS fonts within STOS.      *
40 rem  *                                                                    *
50 rem  *             Original       :   22/02/1998                          *
60 rem  *             Last edited    :   02/04/1998                          *
70 rem  *                                                                    *
80 rem  *           ½ 1997, 1998 Anthony Hoskin.                             *
90 rem  *                        45 Wythburn Rd,                             *
100 rem *                        Newbold,                                    *
110 rem *                        Chesterfield,                               *
120 rem *                        Derbyshire,                                 *
130 rem *                        S41 8DP  (U.K.)                             *
140 rem *                                                                    *
150 rem *   This demo will only work when the following extension(s) are     *
160 rem *   installed :-                                                     *
170 rem *                                                                    *
180 rem *       ST(e)/TT/Falcon Gemtext => v0.8 ½ Anthony Hoskin.            *
190 rem *                                                                    *
200 rem *  Bank usage: GEM/GDOS font is held in bank #5.                     *
210 rem *                                                                    *
220 rem **********************************************************************
230  : 
240 key off : hide on : flash off : curs off 
250 if mode<2 then mode 0 : palette $0,$772,$5,$666,,,,,,,,,,,, else locate 1,8 : centre "Due to the colour graphics used in this demo" : print : print : centre "FONTDEM.BAS doesn't support HI-REZ, sorry." : wait 150 : default : end 
260  : 
270 rem ~~~~~~~~~~~~~~ Initialise vars and strings ~~~~~~~~~~~~~~~~~~~~~~~~~~
280  : 
290 dim MESSAGE$(9)
300 MESSAGE$(0)="STOS BASIC"
310 MESSAGE$(1)="and the"
320 MESSAGE$(2)="GEMTEXT"
330 MESSAGE$(3)="Extension"
340 MESSAGE$(4)="GEM Fonts demo."
350 MESSAGE$(5)="v1.3 "+chr$(189)+" Anthony Hoskin."
360 MESSAGE$(6)="Press any key or click mouse to exit."
370  : 
380 NO_EFFECTS=0
390 BOLD=1
400 ITALIC=4
410 OUTLINED=16
420  : 
430 SCR_WIDTH=640/divx
440 SCR_HEIGHT=400/divy
450  : 
460 rem ~~~~~~~~~~~~~~ Initialise display ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
470  : 
480 ink 2
490 rbar 0,0 to SCR_WIDTH-1,SCR_HEIGHT-1
500 ink 3
510 for L=0 to 3
520  : rbox 0+L,0+L to SCR_WIDTH-1-L,SCR_HEIGHT-1-L
530 next L
540  : 
550 rem ~~~~~~~~~~~~~~ Display the message strings ~~~~~~~~~~~~~~~~~~~~~~~~~~
560  : 
570 gemtext font 5 : rem Use GEM/GDOS font in bank #5.
580 gemtext color 1,0 : rem Foreground and Background colours of text.
590 gemtext mode 0 : rem Use replace mode.
600 gemtext style ITALIC : rem Use Italic text effects.
610 gemtext scale 0 : rem No scaling (normal height text).
620  : 
630 N=0 : Y=8 : gosub 960 : rem Display message$(0).
640 N=1 : Y=40 : gosub 960 : rem Display message$(1).
650  : 
660 gemtext color 3,0 : rem Foreground and Background colours of text.
670 gemtext mode 1 : rem Use transparent mode.
680 gemtext style OUTLINED : rem Use OUTLINED text effects.
690 N=2 : Y=72 : gosub 960 : rem Display message$(2).
700  : 
710 gemtext font 3 : rem Use font #3 (standard ST high-rez 8x16 font).
720 gemtext style NO_EFFECTS : rem Use normal text (no effects).
730 N=3 : Y=100 : gosub 960 : rem Display message$(3).
740  : 
750 gemtext font 1 : rem Use font #1 (standard ST 6x6 font).
760 gemtext style NO_EFFECTS : rem Use normal text (no effects).
770 N=4 : Y=120 : gosub 960 : rem Display message$(4).
780  : 
790 gemtext font 2 : rem Use font #2 (standard ST 8x8 font).
800 gemtext color 1,0 : rem Foreground and Background colours of text.
810 gemtext style OUTLINED : rem Use OUTLINED text effects.
820 N=5 : Y=136 : gosub 960 : rem Display message$(5).
830  : 
840 gemtext color 3,0 : rem Foreground and Background colours of text.
850 gemtext style NO_EFFECTS : rem Use normal text (no effects).
860 N=6 : Y=160 : gosub 960 : rem Display message$(6).
870  : 
880 while (mouse key=false) and (inkey$="") : wend 
890 default : end 
900  : 
910 rem ====================================================================
920 rem Subroutine to determine pixel width of the message$(N)
930 rem and centre it on the screen at the coords X,Y.
940 rem ====================================================================
950  : 
960 STRW=gemtext stringwidth(MESSAGE$(N))
970 X=(SCR_WIDTH-STRW)/2
980 gemtext X,Y,MESSAGE$(N)
990 return 
