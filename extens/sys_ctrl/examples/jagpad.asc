10 rem  **********************************************************************
20 rem  *                                                                    *
30 rem  * A test program to read the Jaguar Controller Pad on the STe/Falcon *
40 rem  *                                                                    *
50 rem  *             Original       :   02/06/1997                          *
60 rem  *             Last edited    :   02/04/1998                          *
70 rem  *                                                                    *
80 rem  *           ½ 1997, 1998 Anthony Hoskin.                             *
90 rem  *                        45 Wythburn Rd,                             *
100 rem *                        Newbold,                                    *
110 rem *                        Chesterfield,                               *
120 rem *                        Derbyshire,                                 *
130 rem *                        S41 8DP  (U.K.)                             *
140 rem *                                                                    *
150 rem *   This demo will only work when the following extension(s) are     *
160 rem *   installed :-                                                     *
170 rem *                                                                    *
180 rem *   ST(e)/TT/Falcon 030 System Control Extension => v0.9             *
190 rem *                                                                    *
200 rem * If this program is run on a machine which does not support the     *
210 rem * enhanced joystick ports i.e. if the machine is not a STe or a      *
220 rem * MegaSte or a Falcon 030 then all the jagpad commands will return   *
230 rem * NULL values. This allows ST users to include these commands in     *
240 rem * anticipation of them running on STe's without their own systems    *
250 rem * crashing.                                                          *
260 rem *                                                                    *
270 rem ***********************************************************************
280  : 
290 on error goto 920
300  : 
310 rem ~~~~~~~~~~~~~~~~~ Initialise Screen mode ~~~~~~~~~~~~~~~~~~~~~~~~~
320  : 
330 key off : rem Turn off the STOS editor window.
340 hide on : rem HIDE the STOS mouse.
350 curs off : rem Turn of the flashing cursor.
360 if mode<2 then mode 1
370  : 
380 rem ~~~~~ Determine machine from cookie jar (if present) ~~~~~~~~~~~~~
390  : 
400 STE=$10000 : MEGA_STE=$10010 : FALCON=$30000 : MACHINE=cookie("_MCH")
410  : 
420 rem ~~~~~ If running on a Falcon 030 set CPU to highspeed ~~~~~~~~~~~~
430  : 
440 if MACHINE=FALCON then _cpucache on : _cpuspeed 16 : _blitterspeed 16
450  : 
460 rem ~~~~~~~~~~~~~~~~~ Initialise display ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
470  : 
480 windopen 1,0,0,80,25,3 : curs off 
490 title " Jaguar Controller Pad Test " : clw : cdown
500  : 
510 if (MACHINE=STE) or (MACHINE=MEGA_STE) or (MACHINE=FALCON) then goto 580
520  : 
530 locate 0,1
540 centre "This machine does not support the enhanced joystick ports."
550 print 
560 centre "In this case the jagpad commands are returning NULL values."
570  : 
580 locate 1,5 : print " Testing 'D=_jagpad direction(0)' ...."
590 locate 1,7 : print " Testing 'F=_jagpad fire(0,0)'    ...."
600 locate 1,9 : print " Testing 'F=_jagpad fire(0,1)'    ...."
610 locate 1,11 : print " Testing 'F=_jagpad fire(0,2)'    ...."
620 locate 1,13 : print " Testing 'P=_jagpad pause(0)'     ...."
630 locate 1,15 : print " Testing 'O=_jagpad option(0)'    ...."
640 locate 1,17 : print " Testing 'K$=_jagpad key$(0)'     ...."
650 locate 1,21 : centre "Press mousekey/ESC to exit."
660  : 
670 while (mouse key=false) and (inkey$<>chr$(27))
680  : D=_jagpad direction(0)
690  : F0=_jagpad fire(0,0)
700  : F1=_jagpad fire(0,1)
710  : F2=_jagpad fire(0,2)
720  : P=_jagpad pause(0)
730  : O=_jagpad option(0)
740  : K$=_jagpad key$(0)
750  : locate 41,5 : print using "Direction  : ##";D
760  : locate 41,7 : print using "Fire btn A : -#";F0
770  : locate 41,9 : print using "Fire btn B : -#";F1
780  : locate 41,11 : print using "Fire btn C : -#";F2
790  : locate 41,13 : print using "Pause btn  : -#";P
800  : locate 41,15 : print using "Option btn : -#";O
810  : if K$<>"" then locate 41,17 : print "Key press  :  "; : print K$
820  : if K$="" then locate 41,17 : print "Key press  :        "
830 wend 
840  : 
850 if MACHINE=FALCON then _cpuspeed 8 : _blitterspeed 8
860 default : clear key : end 
870  : 
880 rem ==================================================================
890 rem General purpose error handler.
900 rem ==================================================================
910  : 
920 on error goto 0
930 physic=default physic : logic=default logic : back=default back
940 break on : if mode<2 then mode 1
950 print "Error #"+str$(errn)-" "; : print " at line #"+str$(errl)-" "
960 print : print "Press any key to Quit."
970 wait key : resume 850
980 end 
